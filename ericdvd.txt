0 REM E.R.I.C. POP #1 PLAYER BY KEVIN SAVETZ - V1.1, ADDS DVD SUPPORT. JUNE 2018
1 DVD=1:REM FOR USE W/PIONEER LV-4400 OR COMPAT. LASERDISC PLAYER (DVD=0) OR PIONEER DVD-V8000 DVD PLAYER (DVD=1)
2 XIO 36,#1,13,0,"R:":REM 4800 8N1
3 XIO 38,#1,0,0,"R:":REM DEFAULT TRANSLATION
4 OPEN #1,13,0,"R:"
5 XIO 40,#1,0,0,"R:":REM START CONCURRENT MODE
6 OPEN #2,4,0,"K:":DIM A$(8)
7 IF DVD THEN GOSUB 8000:? #1;"CH1SE":INPUT #1;A$:REM JUMP TO VIDEO IN CHAPTER 1
9 GOTO 500
10 REM *** JUMP TO FRAME
11 GOSUB 8000
12 IF DVD THEN E=E-6:IF S>3 THEN S=S-1
13 REM ADJUST S AND E TO CORRECT FOR DIFFERENCS IN DVD FRAME NUMBERING VS. LASERDISC
15 IF S=0 THEN 25:REM IF S=0, PLAY FROM CURRENT LOCATION, DON'T SEEK FIRST
18 ? #1;"FR";S;"SE"
20 INPUT #1;A$
25 RETURN 
30 REM *** PLAY S TO E, NONINTERRUPTABLE
35 GOSUB 10
40 ? #1;"FR";E;"PL"
45 INPUT #1;A$
50 RETURN 
60 REM *** PLAY S TO E, INTERRUPTALE
65 GOSUB 10
70 ? #1;"FR";E;"PL"
75 POKE 764,255
80 IF PEEK(53279)=6 THEN KEY=999:RETURN :REM START BUTTON
85 IF PEEK(764)=255 THEN 115
90 GET #2,KEY:IF KEY<>27 THEN RETURN 
95 POP :POP :KEY=0:GOTO 1160:REM ESC = MAIN MENU
115 STATUS #1,X
120 IF PEEK(747)+PEEK(748)=0 THEN 80:REM LOOP UNTIL ENTIRE SEGMENT SHOWN
130 KEY=0:RETURN :REM SEGMENT FINISHED WITHOUT KEYPRESS
150 REM *** WAIT A WHILE FOR KEYPRESS
155 POKE 764,255
160 X=400:KEY=0
165 IF PEEK(53279)=6 THEN KEY=999:RETURN 
170 IF PEEK(764)=255 THEN 175
171 GET #2,KEY:IF KEY<>27 THEN RETURN 
172 POP :POP :KEY=0:GOTO 1160:REM ESC = MAIN MENU
175 X=X-1
180 IF X>0 THEN 165
185 RETURN 
500 S=2191:REM BEGIN ATTRACT MODE
505 GOSUB 8000
506 ? #1;"4RA1DS":INPUT #1;A$:? #1;"1PR":INPUT #1;A$:? #1;"PRESS ANY KEY":INPUT #1;A$:? #1;"TO BEGIN":INPUT #1;A$
507 E=3090:GOSUB 60:REM LOOP COMMERCIAL UNTIL KEYPRESS
508 EXPERIENCE=0:MENUS=0
510 IF KEY=0 THEN 500
520 ? #1;"4RA0DS":INPUT #1;A$:S=3562:E=4992:GOSUB 60:REM FAMILY INTRO
530 IF KEY=0 THEN GOSUB 150:REM WAIT FOR KEYPRESS
540 IF KEY=0 THEN S=0:E=5102:GOSUB 60:REM GO AHEAD, PRESS A KEY
550 IF KEY=0 THEN GOSUB 150:REM WAIT FOR KEYPRESS
560 IF KEY=0 THEN S=1801:GOTO 505:REM NO REPLY, BACK TO ATTRACT MODE VIA "MORE TIME"
570 S=5103:E=5973:GOSUB 60:REM FAUCET
580 IF KEY=0 THEN GOSUB 150
590 IF KEY=0 THEN S=0:E=6056:GOSUB 60
600 IF KEY=0 THEN GOSUB 150
610 IF KEY=0 THEN S=1801:GOTO 505:REM NO REPLY, BACK TO ATTRACT MODE VIA "MORE TIME"
620 IF KEY<>77 THEN S=5974:E=6056:GOSUB 60:GOTO 580:REM 77=M
630 S=6057:E=6986:GOSUB 60:REM MUSIC DEMO
640 IF KEY=0 THEN GOSUB 150
650 IF KEY=0 THEN S=0:E=7058:GOSUB 60
660 IF KEY=0 THEN GOSUB 150
670 IF KEY=0 THEN S=1801:GOTO 505:REM NO REPLY, BACK TO ATTRACT MODE VIA "MORE TIME"
680 IF KEY<>999 THEN S=7059:E=7257:GOSUB 60:GOTO 640
690 S=7258:E=8339:GOSUB 60:REM ASTRONOMY DEMO
700 IF KEY=0 THEN GOSUB 150
710 IF KEY=0 THEN S=1801:GOTO 505:REM NO REPLY, BACK TO ATTRACT MODE VIA "MORE TIME"
720 IF KEY=89 THEN EXPERIENCE=1
730 REM DIDNT TYPE Y, ASSUME NONEXPERIENCED, EXPERIENCE = 0
740 S=8340:E=11754:GOSUB 60:REM OVERVIEW OF APPS, FIRST TIME SHOWING MAIN MENU
745 MENUS=1
750 REM *** PROCESS MAIN MENU
755 MENUS=MENUS+1
760 IF KEY=0 THEN GOSUB 150
770 IF KEY=0 THEN S=11755:E=11897:GOSUB 60:REM STILL THINKING?
780 IF KEY=0 THEN GOSUB 150
790 IF KEY=0 THEN S=1801:GOTO 505:REM NO REPLY, BACK TO ATTRACT MODE VIA "MORE TIME"
800 IF KEY<49 OR KEY>54 THEN S=11898:E=12022:GOSUB 60:GOTO 760:REM ONLY ALLOW KEYS 1-6
810 ON KEY-48 GOSUB 1000,2000,3000,4000,5000,6000
1000 REM HOME STUDY
1010 S=26896:E=28953:GOSUB 60
1020 IF KEY=0 THEN GOSUB 150
1030 IF KEY=0 THEN S=1801:GOTO 505:REM NO REPLY, BACK TO ATTRACT MODE VIA "MORE TIME"
1050 IF KEY=65 THEN 1110
1060 S=29239:E=30355:GOSUB 60
1070 IF KEY=0 THEN GOSUB 150
1100 S=30356:E=30970:GOSUB 60:GOTO 1020:REM OTHER HOME STUDY PROGRAM?
1110 S=31250:E=32240:GOSUB 60
1120 IF KEY=0 THEN GOSUB 150
1140 S=32241:E=32940:GOSUB 60
1150 IF MENUS<2 THEN S=0:E=33019:GOSUB 60:GOTO 750
1160 ? #1;"4RA0DS":INPUT #1;A$:S=33135:GOSUB 10:GOTO 750:REM ADD CONCLUSION ITEM
2000 REM HOME ENTERTAINMENT
2010 S=33155:E=34640:GOSUB 60
2020 IF KEY=0 THEN GOSUB 150
2030 IF KEY=0 THEN S=34641:E=34714:GOSUB 30:REM PRESS S
2035 IF KEY=0 THEN GOSUB 150
2110 S=34715:E=35721:GOSUB 60
2120 IF KEY=0 THEN GOSUB 150
2140 S=35722:E=37663:GOSUB 60
2150 IF MENUS<2 THEN S=0:E=37742:GOSUB 60:GOTO 750
2160 S=37865:GOSUB 10:GOTO 750:REM ADD CONCLUSION ITEM
3000 REM HOME MANAGEMENT
3010 S=19140:E=21387:GOSUB 60
3020 IF KEY=0 THEN GOSUB 150
3030 IF KEY=0 THEN S=1801:GOTO 505:REM NO REPLY, BACK TO ATTRACT MODE VIA "MORE TIME"
3040 IF KEY<>87 AND KEY<>70 THEN S=21388:E=21593:GOSUB 60:GOTO 3020
3050 IF KEY=87 THEN 3100:REM W FOR WORD PROCESSOR
3060 S=23854:E=25424:GOSUB 60:REM HOME FILING MANAGER
3065 IF KEY=0 THEN GOSUB 150
3070 S=25425:E=26649:GOSUB 60
3080 IF MENUS<2 THEN S=26650:E=26729:GOSUB 60:GOTO 750
3090 S=26650:GOSUB 10:GOTO 750:REM ADD CONCLUSION ITEM
3100 S=21594:E=23247:GOSUB 60:REM WORD PROCESSOR
3110 IF KEY=0 THEN GOSUB 150
3120 S=0:E=23339:GOSUB 60
3130 S=23477:E=23636:GOSUB 60:GOTO 3020
3150 REM IF MENUS<2 THEN S=0:E=26729:GOSUB 60:GOTO 750
3160 REM S=26650:GOSUB 10:GOTO 750:REM ADD CONCLUSION ITEM
4000 REM PROGRAMMING
4010 S=37884:E=40039:GOSUB 60
4020 IF KEY=0 THEN GOSUB 150
4030 IF KEY=0 THEN S=1801:GOTO 505
4050 S=40040:E=41866:GOSUB 60
4060 IF MENUS<2 THEN S=0:E=41945:GOSUB 60:GOTO 750
4070 S=42068:GOSUB 10:GOTO 750:REM ADD CONCLUSION ITEM
5000 REM EQUIPMENT
5010 S=12036:E=15952:GOSUB 60
5020 IF KEY=69 THEN 5010:REM E TO REPEAT
5030 S=15968:E=18724:GOSUB 60
5040 IF KEY=80 THEN 5030:REM P TO REPEAT
5060 IF MENUS<2 THEN S=0:E=18922:GOSUB 60:GOTO 750
5070 S=19118:GOSUB 10:GOTO 750:REM ADD CONCLUSION ITEM
6000 REM CONCLUSION
6010 ? "MENUS=";MENUS
6020 ? "EXPERIENCE=";EXPERIENCE
6030 IF EXPERIENCE=0 AND MENUS<2 THEN S=42186:E=42804
6040 IF EXPERIENCE=0 AND MENUS>=2 THEN S=42836:E=43755
6050 IF EXPERIENCE=1 THEN S=43779:E=44894
6060 GOSUB 30
6070 GOTO 500
8000 STATUS #1,X:REM CLEAR INPUT BUFFER IF NECESSARY
8010 IF PEEK(747)+PEEK(748)>0 THEN INPUT #1;A$
8020 RETURN 
